// 广告配置文件，各个广告平台的依赖
def TRADPLUS = "tradplus"
def ADMOB = "admob"
def FACEBOOK = "facebook"
def APPLOVIN = "applovin"
def PANGLE = "pangle"
def UNITY = "unity"
def VUNGLE = "vungle"
def IRONSOURCE = "ironsource"
def MINTEGRAL = "mintegral"
def CHARTBOOST = "chartboost"
def FYBER = "fyber"
def BIGO = "bigo"
def YANDEX = "yandex"
def MYTARGET = "mytarget"

def current_platform_array = null

if (ext.has('network')) {
    current_platform_array = ext['network']
}

def isContainPlatform(platform_array, platform) {
    try {
        if (platform_array != null && !platform_array.isEmpty()) {
            return platform_array.contains(platform)
        }
    } catch (Exception e) {
        println "platform_array error : " + e
    }
    return true
}

dependencies {
    // percentlayout广告布局文件在使用
    implementation 'androidx.percentlayout:percentlayout:1.0.0'

    if (isContainPlatform(current_platform_array, TRADPLUS)) {
        // tradplus
        implementation 'androidx.legacy:legacy-support-v4:1.0.0'
        implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
        //TradPlus
        implementation 'com.tradplusad:tradplus:11.9.0.1'
        // tradplus adx
        // adx的广告源adapter，新版本打包平台会默认带上
        implementation 'com.tradplusad:tp_exchange:40.11.9.0.1'
        implementation 'com.google.code.gson:gson:2.8.6'
        // Cross Promotion
        implementation 'com.tradplusad:tradplus-crosspromotion:27.11.9.0.1'
    }

    if (isContainPlatform(current_platform_array, ADMOB)) {
        implementation 'com.google.android.gms:play-services-ads:23.0.0'
        // admob applovin mediation
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.applovin.mediation:google-adapter:23.0.0.1@aar'
            implementation 'com.applovin.mediation:google-ad-manager-adapter:23.0.0.1@aar'
        }
        // tradplus mediation
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-googlex:2.11.9.0.1'
        }
    }

    if (isContainPlatform(current_platform_array, FACEBOOK)) {
        // facebook
        implementation 'com.facebook.android:audience-network-sdk:6.17.0'
        // facebook applovin mediation
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.applovin.mediation:facebook-adapter:6.17.0.0@aar'
        }
        // tradplus mediation
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-facebook:1.11.9.0.1'
        }
        // facebook admob mediation
        if (isContainPlatform(current_platform_array, ADMOB)) {
            implementation 'com.google.ads.mediation:facebook:6.17.0.0@aar'
        }
    }

    if (isContainPlatform(current_platform_array, APPLOVIN)) {
        // AppLovin
        implementation 'com.applovin:applovin-sdk:12.5.0'
        // admob applovin mediation
        implementation 'com.google.ads.mediation:applovin:12.5.0.1@aar'
        // tradplus applovin mediation
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-applovin:9.11.9.0.1'
        }
        // tradplus max mediation
        // if (isContainPlatform(current_platform_array, TRADPLUS)) {
        //     implementation 'com.tradplusad:tradplus-max:51.9.8.0.1'
        // }
    }

    if (isContainPlatform(current_platform_array, PANGLE)) {
        // Pangle
        implementation 'com.pangle.global:ads-sdk:5.9.0.6'
        // Pangle applovin mediation
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.applovin.mediation:bytedance-adapter:5.9.0.6.0@aar'
        }
        // Pangle tradplus mediation
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-pangle:19.11.9.0.1'
        }
        // Pangle admob mediation
        if (isContainPlatform(current_platform_array, ADMOB)) {
            implementation 'com.google.ads.mediation:pangle:5.9.0.6.0@aar'
        }
    }

    if (isContainPlatform(current_platform_array, UNITY)) {
        // unity
        implementation 'com.unity3d.ads:unity-ads:4.12.0'
        // unity applovin mediation
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.applovin.mediation:unityads-adapter:4.12.0.0@aar'
        }
        // tradplus unity mediation
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-unity:5.11.9.0.1'
        }
        // google unity mediation
        if (isContainPlatform(current_platform_array, ADMOB)) {
            implementation 'com.google.ads.mediation:unity:4.12.0.0@aar'
        }
    }

    if (isContainPlatform(current_platform_array, VUNGLE)) {
        // vungle
        implementation 'com.vungle:vungle-ads:7.3.2'
        // applovin vungle mediation
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.applovin.mediation:vungle-adapter:7.3.2.2'
        }
        // tradplus vungle mediation
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-vunglex:7.11.9.0.1'
        }
        // google vungle medation
        if (isContainPlatform(current_platform_array, ADMOB)) {
            implementation 'com.google.ads.mediation:vungle:7.3.2.0@aar'
        }
    }

    if (isContainPlatform(current_platform_array, IRONSOURCE)) {
        // ironSource
        implementation 'com.ironsource.sdk:mediationsdk:8.1.0'
        // Vungle applovin mediation
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.applovin.mediation:ironsource-adapter:8.1.0.0.0@aar'
        }
        // tradplus mediation
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-ironsource:10.11.9.0.1'
        }
        // google ironsource mediation
        if (isContainPlatform(current_platform_array, ADMOB)) {
            implementation 'com.google.ads.mediation:ironsource:8.1.0.0@aar'
        }
    }

    if (isContainPlatform(current_platform_array, MINTEGRAL)) {
        // mintegral
        def mintegral_version = "16.7.61"
        implementation "com.mbridge.msdk.oversea:videojs:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:mbjscommon:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:playercommon:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:reward:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:videocommon:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:same:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:interstitialvideo:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:interstitial:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:mbnative:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:nativeex:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:mbbanner:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:mbbid:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:mbnativeadvanced:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:mbsplash:${mintegral_version}"
        implementation "com.mbridge.msdk.oversea:newinterstitial:${mintegral_version}"
        // mintegral applovin mediation
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.applovin.mediation:mintegral-adapter:16.7.61.0@aar'
        }
        // tradplus mediation
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-mintegralx_overseas:18.11.9.0.1'
        }
        // mintegral google mediation
        if (isContainPlatform(current_platform_array, ADMOB)) {
            implementation('com.google.ads.mediation:mintegral:16.7.61.0@aar')
        }
    }

    if (isContainPlatform(current_platform_array, CHARTBOOST)) {
        // chartboost
        implementation 'com.chartboost:chartboost-sdk:9.7.0'
        implementation 'com.chartboost:chartboost-mediation-sdk:4.7.1'
        implementation 'com.chartboost:chartboost-mediation-adapter-chartboost:4.9.5.0.0'
        // chartboost applovin mediation
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.applovin.mediation:chartboost-adapter:9.7.0.0'
        }
        // chartboost tradplus mediation
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-helium:30.11.9.0.1'
        }
        // chartboost admob mediation
        if (isContainPlatform(current_platform_array, ADMOB)) {
            implementation('com.google.ads.mediation:chartboost:9.7.0.0@aar')
        }
    }

    if (isContainPlatform(current_platform_array, FYBER)) {
        //  fyber
        implementation 'com.fyber:marketplace-sdk:8.2.7'
        // fyber applovin mediation
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.applovin.mediation:fyber-adapter:8.2.7.1'
        }
        // fyber tradplus mediation
        // tradplus未更新adapter，会导致crash
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-fyber:24.11.9.0.1'
        }
        // fyber admob mediation
        if (isContainPlatform(current_platform_array, ADMOB)) {
            implementation('com.google.ads.mediation:fyber:8.2.7.0@aar')
        }
    }

    if (isContainPlatform(current_platform_array, BIGO)) {
        // Bigo
        implementation 'com.bigossp:bigo-ads:4.8.0'
        // applovin Bigo mediation
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.bigossp:max-mediation:4.8.0.0'
        }
        // tradplus Bigo mediation
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-bigo:57.11.9.0.1'
        }
        // google bigo mediation
        if (isContainPlatform(current_platform_array, ADMOB)) {
            implementation 'com.bigossp:admob-mediation:4.8.0.0@aar'
        }
    }

    if (isContainPlatform(current_platform_array, YANDEX)) {
        // Yandex
        implementation 'com.yandex.android:mobileads:7.1.0'
        // applovin
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.applovin.mediation:yandex-adapter:7.1.0.0'
        }
        // tradplus
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-yandex:50.11.9.0.1'
        }
    }

    if (isContainPlatform(current_platform_array, MYTARGET)) {
        // MyTarget
        implementation 'com.my.target:mytarget-sdk:5.21.0'
        // applovin
        if (isContainPlatform(current_platform_array, APPLOVIN)) {
            implementation 'com.applovin.mediation:mytarget-adapter:5.21.0.0'
        }
        // tradplus
        if (isContainPlatform(current_platform_array, TRADPLUS)) {
            implementation 'com.tradplusad:tradplus-mytarget:33.11.9.0.1'
        }
    }
}